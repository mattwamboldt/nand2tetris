// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Perform our only calculation
    Inc16(in=old, out=incremented);
    
    //Figure out the which input value
    Mux16(a=old, b=incremented, sel=inc, out=in1);
    Mux16(a=in1, b=in, sel=load, out=in2);
    Mux16(a=in2, b=false, sel=reset, out=val);
    
    // Since the 3 flags cause a load and loading the old value doesn't matter
    // We can skip muxing to find out if we load and just always do so
    Register(in=val, load=true, out=out, out=old);
}
